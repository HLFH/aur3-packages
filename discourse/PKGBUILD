# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>

pkgname=discourse
pkgver=1.1.0
pkgrel=2
pkgdesc="A simple, flat forum, where replies flow down the page in a line"
arch=('i686' 'x86_64')
url="http://www.discourse.org/"
license=('GPL')
depends=('ruby>=2.0' 'ruby-bundler>=1.5.2' 'gmp' 'libxml2' 'libxslt' 'openssl'
         'krb5' 'libgcrypt' 'libgpg-error' 'postgresql-libs' 'xz' 'zlib')
optdepends=('postgresql'
            'postfix: mail server in order to receive mail notifications'
            'jpegoptim'
            'pngcrush'
            'pngout'
            'optipng')
backup=("etc/webapps/discourse/unicorn.conf.rb"
        "etc/webapps/discourse/discourse.conf")
source=("${pkgname}::git+https://github.com/discourse/discourse.git#tag=v${pkgver}"
        "discourse-sidekiq.service"
        "discourse-unicorn.service"
        "discourse.target"
        "discourse.tmpfiles.d"
        "discourse.logrotate"
        "unicorn.conf.rb"
        "apache.conf.example"
        "apache-ssl.conf.example"
        "nginx.conf.example"
        "nginx-ssl.conf.example")
install="${pkgname}.install"
sha256sums=('SKIP'
            '8bed2a6cc9cc9ed8a3ddba84d8ccd2b776af25b293b5b29c48e8578a7dac4c31'
            'ec711a14712cd7419378a92209b9db1af6a26f758f24aeddcf6e73efc763cd9b'
            'b512d219056537f7a7b749a10cfb90fb78116d7c6414e0a0bc72c08f1ced3f43'
            '71fa450935dea04c30cc5a0eaaa16012ac765445644ca719b7a1d589aad1938f'
            '0351c72ebef44ca7d03923ed28f03b75ac8079cfd4d213189ac9994672ca8296'
            '9a6602d8a0237fa1a18099a9abd3e81183404535863049a45f3238df56562ce9'
            'b40c615d8da35fe5a02b914baf5920025ee7167b5d78b47d5905e633934ca11e'
            '8056354bac606f66a3667034ff93ffbfce3ec52664e7a809835d5cc45a121c30'
            '7d00b5d99777b4843cdc2f8fb9c65beb94375dcaa6351d2c8f6d55161512b3c5'
            'cd8965f971047ce5cf5c32d4709fe231c392893702db181300b02de86e0f70fb')

_homedir="/var/lib/${pkgname}"
_datadir="/usr/share/webapps/${pkgname}"
_logdir="/var/log/${pkgname}"
_etcdir="/etc/webapps/${pkgname}"
_systemddir="/usr/lib/systemd/system"

prepare() {
  cd "${srcdir}/${pkgname}"

  # Patch source files to include a static revision string and remove git repo.
  # Including the git repository triples the package size.
  _revision=$(git log --pretty=format:%h -n 1)
  msg2 "Patching discourse revision number ${_revision} to a static string..."
  sed -i "s|<%= Discourse.git_version %>|${_revision}|g" "app/views/layouts/_head.html.erb"
  rm -rf .git

  msg2 "Patching discourse_defaults.conf to serve assets..."
  sed -i "s|serve_static_assets = false|serve_static_assets = true|g" "config/discourse_defaults.conf"
}

build() {
  cd "${srcdir}/${pkgname}"

  cpus=$(nproc)
  msg2 "Starting bundler with ${cpus} jobs..."
  msg2 "Gems will be installed into vendor/bundle."
  bundle config build.nokogiri --use-system-libraries
  bundle install --jobs ${cpus} --no-cache --no-prune --deployment --without development test
}

package() {
  cd "${srcdir}/${pkgname}"

  install -d "${pkgdir}/usr/share/webapps"
  cp -r "${srcdir}/${pkgname}" "${pkgdir}${_datadir}"

  msg2 "Creating directories and symlinks..."
  install -d \
    "${pkgdir}${_etcdir}" \
    "${pkgdir}/usr/share/doc/${pkgname}" \
    "${pkgdir}${_homedir}" \
    "${pkgdir}/usr/lib/systemd/system"

  mv "${pkgdir}${_datadir}/public" "${pkgdir}${_homedir}/public" && \
    ln -fs "${_homedir}/public" "${pkgdir}${_datadir}/public"
  mv "${pkgdir}${_datadir}/plugins" "${pkgdir}${_homedir}/plugins" && \
    ln -fs "${_homedir}/plugins" "${pkgdir}${_datadir}/plugins"
  # We are using PrivateTmp=true to start unicorn, so this is safe!
  rm -rf "${pkgdir}${_datadir}/tmp" && ln -sf "/var/tmp" "${pkgdir}${_datadir}/tmp"
  rm -rf "${pkgdir}${_datadir}/log" && ln -sf "${_logdir}" "${pkgdir}${_datadir}/log"

  msg2 "Install and symlink config files..."
  install -Dm0644 "${srcdir}/unicorn.conf.rb" "${pkgdir}/${_etcdir}/unicorn.conf.rb"
  cp "config/discourse_quickstart.conf" "config/discourse.conf"
  for _file in "discourse.conf" "discourse_defaults.conf"; do
    mv "config/${_file}" "${pkgdir}${_etcdir}"
    ln -fs "${_etcdir}/${_file}" "${pkgdir}${_datadir}/config/"
  done

  msg2 "Installing license, documentation and example files..."
  mv "README.md" "CONTRIBUTING.md" "COPYRIGHT.txt" "${pkgdir}/usr/share/doc/${pkgname}"
  cp ${srcdir}/*.conf.example "${pkgdir}/usr/share/doc/${pkgname}"
  install -D "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  msg2 "Installing systemd service files..."
  install -Dm0644 "${srcdir}/discourse-unicorn.service" "${pkgdir}/${_systemddir}/discourse-unicorn.service"
  install -Dm0644 "${srcdir}/discourse-sidekiq.service" "${pkgdir}/${_systemddir}/discourse-sidekiq.service"
  install -Dm0644 "${srcdir}/discourse.target" "${pkgdir}/${_systemddir}/discourse.target"

  msg2 "Installing systemd tmpfiles.d..."
  install -Dm0644 "${srcdir}/discourse.tmpfiles.d" "${pkgdir}/usr/lib/tmpfiles.d/discourse.conf"

  msg2 "Installing logrotate..."
  install -Dm0644 "${srcdir}/discourse.logrotate" "${pkgdir}/etc/logrotate.d/discourse"
}
