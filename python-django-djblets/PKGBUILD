# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>

pkgbase="python-django-djblets"
pkgname=('python-django-djblets' 'python2-django-djblets')
_pkgname="djblets"
pkgver=0.8.8
pkgrel=1
pkgdesc="A collection of useful extensions for Django"
license=('MIT')
arch=('any')
url="https://github.com/djblets/djblets"
source=("https://github.com/djblets/djblets/archive/release-${pkgver}.tar.gz"
        "0001-Replace-pillowfight-with-Pillow.patch"
        "0002-Fixed-build-error.patch")
md5sums=('c890f7f735bb945066525a6386908267'
         'abd532484bfb35a330d75db7c8b6fb56'
         '61d9d8eacc24ab5dfe539e0942ecc56f')
sha256sums=('3acdddf4bd01cb8b1d3e4815a58f5c1a4921cab24fc401688b95304b454ba31b'
            '8382c2eb3cbafa661fff997332882ee5ff36576564a36cc0523cc6f2ba2773de'
            '90c95ac9ab9f6aa38be92a991d7e4f57f842bfb33a711aef932333798f2777bf')

prepare() {
  cd "${srcdir}/${_pkgname}-release-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Replace-pillowfight-with-Pillow.patch"
  patch -p1 -i "${srcdir}/0002-Fixed-build-error.patch"
}

package_python-django-djblets() {
  depends=('python-django' 'python-feedparser' 'python-pytz' 'python-pillow'
           'python-django-pipeline' 'nodejs-uglify-js')
  makedepends=('python-setuptools' 'lessc')

  cd "${srcdir}/${_pkgname}-release-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

package_python2-django-djblets() {
  depends=('python2-django' 'python2-feedparser' 'python2-pytz' 'python2-pillow'
           'python2-django-pipeline' 'nodejs-uglify-js')
  makedepends=('python2-setuptools' 'lessc')

  cd "${srcdir}/${_pkgname}-release-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}
