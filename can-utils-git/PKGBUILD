# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=can-utils-git
_pkgname=can-utils
pkgver=r228.518faef
pkgrel=1
pkgdesc="SocketCAN utilities"
arch=('i686' 'x86_64')
url="https://gitorious.org/linux-can/can-utils"
license=('GPLv2')
makedepends=(git libtool)
provides=can-utils
conflicts=can-utils
source=(git+https://git.gitorious.org/linux-can/can-utils.git)
md5sums=('SKIP')


pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="$pkgdir/" install
}
